#!/usr/bin/perl -w

use strict;
use lib $ENV{'AARON'};
use lib $ENV{'PERL_LIB'};

use Getopt::Long;
use Pod::Usage;
use AaronTools::Catalysis;
use Data::Dumper;

unshift @INC, ("$ENV{'AARON'}/bin");
require _utils;

my $debug;

sub main {
    my $file = shift;
    my %opt  = @_;

    # if substituent is named, just go ahead and pass that to substitute()
    # otherwise, we should pass substituent numbering to Catalysis class
    # to preserve indexing when interacting with object on our side
    my %numbered;
    $numbered{ligand}    = {};
    $numbered{substrate} = {};
    my %named;
    $named{ligand}    = {};
    $named{substrate} = {};

    # store command line options in the appropriate hash
    ( $numbered{ligand}, $named{ligand} ) = store_requested( \%opt, 'ligand' );
    ( $numbered{substrate}, $named{substrate} ) =
      store_requested( \%opt, 'substrate' );
    if ($debug) {
        print "NUMBERED\n", Dumper(%numbered), "\n";
        print "NAMED\n",    Dumper(%named),    "\n";
    }

    # create catalysis object with requested substituent indexing
    my $geom = _utils::get_cat( $file, \%numbered, $opt{relnum} );
    return 1 unless $geom;

    # perform substitute for each substituent
    foreach my $component ( keys %numbered ) {
        # determine requested target atom, in aaron's number scheme
        my %params;
        my %convert = _utils::get_sub_numbers( $geom, $component );
        while ( my ( $target, $aaron_num ) = each %convert ) {
            map { $params{$_} = $opt{$component}{$target}; } split /,/,
              $aaron_num;

        }
        if ($debug) {
            print "PARAMS\n", Dumper(%params), "\n";
        }

        # do substitution
        $geom->substitute( $component, %params );
    }
    foreach my $component ( keys %named ) {
        $geom->substitute( $component, %{ $named{$component} } );
    }

    # Printing
    # prints to STDOUT if $outfile == ''
	$opt{outfile} = _utils::handle_overwrite($opt{outfile});
    print("Saving substituted coords to $opt{outfile}\n") if $opt{outfile};
    $geom->printXYZ( $opt{outfile}, '', 1 );
}

# store atoms requested for substitution in the appropriate hash
# this allows us to label atoms that may be moved around during the process
sub store_requested {
    my %opt       = %{ shift @_ };
    my $component = shift;
    my %numbered;
    my %named;

    foreach my $sub ( keys %{ $opt{$component} } ) {
        if ( $sub =~ /^\d+,/ ) {
            map { $numbered{ $_ - 1 } = $sub } split( /,/, $sub );
        } elsif ( $sub =~ /^\d+/ ) {
            $numbered{ $sub - 1 } = $sub;
        } else {
            $named{$sub} = $opt{$component}{$sub};
        }
    }

    return \%numbered, \%named;
}

# read in options
my %opt;
my %sublist;
my %liglist;
$opt{substrate} = \%sublist;
$opt{ligand}    = \%liglist;
$opt{outfile}   = '';
GetOptions(
    'help|h'            => \$opt{help},
    'debug|d'           => \$debug,
    'relnum|r'          => \$opt{relnum},
    'substrate|s=s%{,}' => \%sublist,
    'ligand|l=s%{,}'    => \%liglist,
    'output|o:s'        => \$opt{outfile} )
  or pod2usage( {
      -exitval => 1,
      -verbose => 1
  } );
pod2usage(0) if $opt{help};

# need to supply a geometry file
pod2usage( {
        -message => "Please provide one geometry file to substitute",
        -exitval => 1,
        -verbose => 1
    } ) unless ( $#ARGV == 0 );

# target supplied as 1-indexed, Aaron uses 0-indexing

&main( $ARGV[0], %opt );

=pod

=head1 SYNOPSIS

cat_substitute [options] [-l|s target=sub ...] file

=head1 OPTIONS

=over

=item B<-l target=sub>, B<--ligand target=sub>

Replace target (atom or substituent) on ligand with specified substituent

=item B<-s target=sub>, B<--substrate target=sub>

Replace target (atom or substituent) on substrate with specified substituent

=item B<-r>, B<--relnum>

Use relative ligand/substrate numbering

=item B<-o outfile>, B<--output outfile>

Output to file instead of STDOUT

=item B<-h>, B<--help>

Print this help message and exit

=back

=cut

